; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#include "./scripts/common.iss"

[Setup]
AppName=LinkerMod
AppVersion=0.0.1
UninstallDisplayIcon={app}\LinkerMod.exe

#if BUILD_TYPE == 'PRODUCTION'
	WizardImageFile=C:\Users\SE2Dev\Pictures\dface_512x512.bmp
	WizardSmallImageFile=C:\Users\SE2Dev\Pictures\dface_512x512.bmp
#endif


[Icons]
Name: "{commondesktop}\Game Mod";	Filename: "{#BinDir}\BlackOps.exe"
; Name: "{group}\LinkerMod\Game_Mod"; Filename: "{#BinDir}\BlackOps.exe"


[Components]
Name: "GameMod";	Description: "Game Mod";	\
					Types: full compact custom;	\
					Flags: fixed

; exlusive flag makes them radio buttons
Name: "LinkerMod"; Description: "Mod Tools"; Types: full custom
Name: "LinkerMod\AssetUtil";	Description: "Asset Util"; Types: full;			Flags:
Name: "LinkerMod\AssetViewer";	Description: "AssetViewer Mod"; Types: full;	Flags:
Name: "LinkerMod\CoD2Map";		Description: "CoD2Map Mod"; Types: full;		Flags:
Name: "LinkerMod\CoD2Rad";		Description: "CoD2Rad Mod"; Types: full;		Flags:
Name: "LinkerMod\Linker";		Description: "Linker Mod"; Types: full;			Flags:
Name: "LinkerMod\Radiant";		Description: "Radiant Mod"; Types: full;		Flags:


[Tasks]
Name: extract;	Description: "Extract Assets";	\
				Components: LinkerMod\AssetUtil;
Name: extract\iwd; 	Description: "Extract I&WDs"; 			\
					Components: LinkerMod\AssetUtil;
Name: extract\iwd\img; 	Description: "Extract &Images"; 	\
						Components: LinkerMod\AssetUtil;
Name: extract\iwd\snd; 	Description: "Extract &Sounds"; 	\
						Components: LinkerMod\AssetUtil;
Name: extract\iwd\raw;	Description: "Extract &Rawfiles"; 	\
						Components: LinkerMod\AssetUtil;

Name: extract\ffs; 		Description: "Extract &FastFiles"; 		\
						Components: LinkerMod\AssetUtil;
Name: extract\ffs\snd; 	Description: "Extract &Sounds"; 		\
						Components: LinkerMod\AssetUtil;
Name: extract\ffs\raw; 	Description: "Extract &Rawfiles"; 		\
						Components: LinkerMod\AssetUtil;
Name: extract\ffs\ents; Description: "Extract &Entity Maps"; 	\
						Components: LinkerMod\AssetUtil;

[Files]
; Source: "README.md"; DestDir: "{app}"; Flags: isreadme

;
; Actual LinkerMod binaries
;
Source: "build\Release\proxy.dll";			DestDir: "{#BinDir}";
Source: "build\Release\game_mod.dll";		DestDir: "{#BinDir}"; Components: GameMod
Source: "build\Release\asset_util.exe";		DestDir: "{#BinDir}"; Components: LinkerMod\AssetUtil
Source: "build\Release\asset_viewer.dll";	DestDir: "{#BinDir}"; Components: LinkerMod\AssetViewer
Source: "build\Release\cod2map.dll";		DestDir: "{#BinDir}"; Components: LinkerMod\CoD2Map
Source: "build\Release\cod2rad.dll";		DestDir: "{#BinDir}"; Components: LinkerMod\CoD2Rad
Source: "build\Release\linker_pc.dll";		DestDir: "{#BinDir}"; Components: LinkerMod\Linker
Source: "build\Release\radiant_mod.dll";	DestDir: "{#BinDir}"; Components: LinkerMod\Radiant

;
; Mod Tools asset files
;

; Utility scripts
Source: "components\scripts\*";		DestDir: "{#BinDir}\scripts";		\
									Components: LinkerMod\AssetUtil; 	\
									Flags: recursesubdirs;

#if BUILD_TYPE == 'PRODUCTION'
; Custom / missing assets
Source: "components\resource\*";	DestDir: "{app}";		\
									Components: LinkerMod;	\
									Flags: recursesubdirs;
#endif

; Test automatic shit
; Source: "{code:GetAutoFiles}";				DestDir: "{#BinDir}\debug}";	Components: Debug; Flags: external recursesubdirs createallsubdirs


[Run]
;; extract-iwd --all --includeLocalized
Filename: "{#BinDir}\asset_util.exe";	StatusMsg: "Extracting IWD assets... {#PleaseWait}";		\
										Parameters: "extract-iwd {code:ExtractIWD_ResolveParams}";	\
										WorkingDir:	"{#BinDir}";									\
										Tasks: extract\iwd;
;										Flags: runhidden;											\
;; extract-ff -v --all --includeLocalized
Filename: "{#BinDir}\asset_util.exe";	StatusMsg: "Extracting fastfile assets... {#PleaseWait}";	\
										Parameters: "extract-ff {code:ExtractFF_ResolveParams}";	\
										WorkingDir:	"{#BinDir}";									\
										Tasks: extract\ffs\snd extract\ffs\raw

Filename: "{#BinDir}\asset_util.exe";	StatusMsg: "Extracting entity prefabs... {#PleaseWait}";	\
										Parameters: "ents --overwrite --dummyBrushes *";			\
										WorkingDir:	"{#BinDir}";									\
										Tasks: extract\ffs\ents

;										Flags: runhidden;							\
; Filename: "{app}\README.TXT"; Description: "View the README file"; Flags: postinstall shellexec skipifsilent

#if BUILD_TYPE == 'PRODUCTION'
Filename: "{#BinDir}\launcher.exe";		Description: "Launch mod tools";					\
										Flags: postinstall nowait skipifsilent unchecked;
#endif


[Code]
procedure InitializeWizard;
begin
end;

function NextButtonClick(curPageID:integer): boolean;
begin
	Result := Com_ValidateInstallPath(curPageID);
end;

//
// Resolve the asset_util parameters for IWD asset extraction
//
function ExtractIWD_ResolveParams(param: String): string;
begin
	Result := ' --overwrite --includeLocalized';

	if IsTaskSelected('extract\iwd\img') then
		Result := Result + ' --images';

	if IsTaskSelected('extract\iwd\snd') then
		Result := Result + ' --sounds';

	if IsTaskSelected('extract\iwd\raw') then
		Result := Result + ' --rawfiles';

	MsgBox('IWD PARAMS: ' + Result, mbError, MB_YESNO);
end;

//
// Resolve the asset_util parameters for fastfile asset extraction
// TODO: Make this auto skip if sound & rawfiles are both empty
//
function ExtractFF_ResolveParams(param: String): string;
begin
	Result := ' -v --overwrite --includeLocalized';

	if IsTaskSelected('extract\ffs\snd') then
		Result := Result + ' --sounds';

	if IsTaskSelected('extract\ffs\raw') then
		Result := Result + ' --rawfiles';

	MsgBox('FF PARAMS: ' + Result, mbError, MB_YESNO);
end;
